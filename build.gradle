plugins {
    id 'java'
    id 'org.gauge' version '2.3.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter:5.+")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    testImplementation 'com.thoughtworks.gauge:gauge-java:+'
    testImplementation 'org.htmlunit:htmlunit:4.16.0'
    testImplementation 'se.fishtank:css-selectors:2.0'
    testImplementation 'org.jsoup:jsoup:1.21.2'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.json:json:20250517'
}

group = 'com.thoughtworks.gauge.test'
version = '0.4.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

import org.gauge.gradle.GaugeTask

def languages = ['java', 'js', 'csharp', 'dotnet', 'ruby', 'python']

languages.each {lang ->
    tasks.register("${lang}FT", GaugeTask) {
        doFirst {
            gauge {
                specsDir = 'specs'
                inParallel = true
                env = "ci-${lang}"
                tags = "${lang}"
                additionalFlags = "--simple-console"
            }
        }
    }

    tasks.register("clean${lang}FT") {
        doFirst {
            delete "reports-${lang}"
            if (lang == 'ruby') {
                delete 'resources/LocalTemplates/ruby/vendor'
                delete 'resources/LocalTemplates/ruby/.buildlock'
                delete 'resources/LocalTemplates/ruby/Gemfile.lock'
            } else {
                delete "resources/LocalTemplates/${lang}"
            }
        }
    }
    clean.dependsOn "clean${lang}FT"
}

tasks.withType(Test).configureEach {
    // https://docs.gradle.org/8.4/userguide/upgrading_version_8.html#test_framework_implementation_dependencies
    // Using JUnitPlatform for running tests
    useJUnitPlatform()
}

tasks.register('cleanGauge') {
    doFirst {
        delete '.gauge'
        delete 'logs'
        delete 'testLogs'
    }
}
clean.dependsOn cleanGauge